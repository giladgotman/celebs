apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.gggames.hourglass"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 33
        versionName "1.0.33"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "general"

    productFlavors {
        dev {
            dimension 'general'
            versionNameSuffix '-dev'
        }
        production {
            dimension 'general'
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {

        }
    }
    androidExtensions {
        experimental = true
    }


    sourceSets {
        String sharedDevDir = 'src/dev/java'
        String sharedTestDir = 'src/sharedTests/java'
        test {
            java.srcDir sharedDevDir
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDirs = ['src/androidTest/java', sharedTestDir]
            resources.srcDirs = ['src/androidTest/res']
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildToolsVersion '30.0.2'

}


configurations {
    ktlint
}
// Runs a lint check on all Kotlin files
task ktlint(type: JavaExec) {
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

// Apply ktlint formatter settings to IntelliJ
task ktlintApplyToIdeaProject(type: JavaExec) {
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "--apply-to-idea-project", "--android", "-y"
    workingDir = rootDir
}

// Check task should run Kotlin linter as well
check.dependsOn ktlint

// Auto-formats all Kotlin files
task ktlintFormat(type: JavaExec) {
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Firebase
    implementation "com.google.firebase:firebase-analytics:$versions.firebase_sdk"
    implementation 'com.google.firebase:firebase-firestore-ktx:21.5.0'


    // rx-java
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxjava"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxandroid"

    //Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //anko
    implementation "org.jetbrains.anko:anko-commons:$versions.anko"
    implementation "org.jetbrains.anko:anko-design:$versions.anko" // For SnackBars

    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha03'

    //DI
    implementation "com.google.dagger:dagger:$versions.dagger"
    kapt "com.google.dagger:dagger-compiler:$versions.dagger"
    kaptAndroidTest "com.google.dagger:dagger-$versions.dagger"

    //deeplinks
    implementation 'com.google.firebase:firebase-dynamic-links-ktx:19.1.0'

    //analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:17.4.4'

    // Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics:17.1.1'

    //Exo player
    implementation 'com.google.android.exoplayer:exoplayer-core:2.10.0'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.10.0'
    implementation 'nl.dionsegijn:konfetti:1.2.2'

    implementation 'me.relex:circleindicator:1.2.2@aar'

    ktlint 'com.pinterest:ktlint:0.35.0'

    androidTestImplementation(
            "com.google.truth:truth:$versions.truth"
    )

    testImplementation(
            "com.google.truth:truth:$versions.truth"
    )

}
